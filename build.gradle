plugins {
  id 'java'
  id 'application'
  id 'jacoco'
  id 'eclipse'
  id "org.owasp.dependencycheck" version "6.0.3"
  id 'com.github.spotbugs' version '4.6.0'
  id 'io.spring.dependency-management' version '1.0.10.RELEASE'
  id 'org.springframework.boot' version '2.4.0'
}

ext {
  nexusBaseUrl = project.hasProperty("nexusBaseUrl") ? project.getProperty("nexusBaseUrl") : "https://nexus.adaptris.net/nexus"
  repoUsername = project.hasProperty("repoUsername") ? project.getProperty("repoUsername") : "unknown"
  repoPassword = project.hasProperty("repoPassword") ? project.getProperty("repoPassword") : "unknown"
  componentName = "Interlok Artifact Downloader"
  organizationName = "Adaptris Ltd"
  organizationUrl = "https://www.adaptris.com"
  /*slf4jVersion = "1.7.30"
  log4j2Version = "2.13.3"*/
  swaggerVersion = "2.1.5"
  ivyVersion = "2.5.0"
  mockitoVersion = "3.6.28"
}

// Override spring boot dependencies versions
ext["tomcat.version"] = "9.0.40"
// ext["jersey.version"] = "2.32"
// ext["slf4j.version"] = "1.7.30"
ext["log4j2.version"] = "2.14.0"
ext["jackson-bom.version"] = "2.12.0"

run.enabled = false
distTar.enabled=false
distZip.enabled=false

sourceCompatibility = JavaVersion.VERSION_1_8
group   = "com.adaptris"
version = "1.0-SNAPSHOT" // Should we follow interlok versions?

repositories {
  mavenCentral()
  maven { url "$nexusBaseUrl/content/groups/public" }
  maven { url "$nexusBaseUrl/content/groups/interlok" }
  maven {
    credentials {
      username repoUsername
      password repoPassword
    }
    url "$nexusBaseUrl/content/groups/private"
  }
}

eclipse {
  classpath {
    downloadJavadoc = true
    downloadSources = true
  }
}

configurations {
  compile {
    exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    exclude group: "org.springframework.boot", module: "spring-boot-starter-data-jpa"
  }
}

// Use static classpath resources (i.e. in src/main/resources by default) in the live application when running bootRun.
bootRun {
  sourceResources sourceSets.main
}

jar {
  baseName = rootProject.name
  version =  version
  manifest {
    attributes "Built-By": System.getProperty("user.name"),
               "Build-Jdk": System.getProperty("java.version"),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName,
               "Implementation-URL": organizationUrl
  }
}

sourceSets {
  test.compileClasspath += configurations.compileOnly
  test.runtimeClasspath += configurations.compileOnly
}

dependencies {
  compile "org.springframework.boot:spring-boot-devtools"
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-log4j2"
  compile "org.springframework.boot:spring-boot-starter-thymeleaf"
  compile "org.springframework.boot:spring-boot-starter-jersey"

  compile "com.google.guava:guava:30.0-jre"
/*
  compile "org.slf4j:slf4j-api:$slf4jVersion"

  compile "org.apache.logging.log4j:log4j-core:$log4j2Version"
  compile "org.apache.logging.log4j:log4j-1.2-api:$log4j2Version"
  compile "org.apache.logging.log4j:log4j-jul:$log4j2Version"
  compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version"
  compile "org.apache.logging.log4j:log4j-api:$log4j2Version"

  compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
  compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
  compile "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
  compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:$jacksonVersion"
  compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVersion"
  compile "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jacksonVersion"
*/
  compile "io.swagger.core.v3:swagger-core:$swaggerVersion"
  compile "io.swagger.core.v3:swagger-jaxrs2:$swaggerVersion"

  compile "org.apache.commons:commons-lang3:3.11"
  compile "commons-io:commons-io:2.8.0"

  compile "org.apache.ivy:ivy:$ivyVersion"

  testCompile "org.springframework.boot:spring-boot-starter-test"
  /*testCompile "org.hamcrest:hamcrest-core:2.2"
  testCompile "junit:junit:4.13.1"
  testCompile "org.mockito:mockito-core:$mockitoVersion"
  testCompile "org.mockito:mockito-inline:$mockitoVersion"*/
}

startScripts {
  mainClassName = "com.adaptris.downloader.Application"
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

spotbugsMain {
  effort = "max"
  reportLevel = "high"
  // showProgress = "true"
  // Ignore failures?
  // ignoreFailures = false
  reports {
    xml {
      enabled = false
    }
    html {
      enabled = true
    }
  }
  // includeFilter = new File("$rootDir/gradle/spotbugs-filter.xml")
  // excludeFilter = new File("$rootDir/gradle/spotbugs-exclude.xml")
}

// disable spotbugsTests which checks our test code..
spotbugsTest.enabled = false

dependencyCheck  {
  suppressionFiles= [ "https://raw.githubusercontent.com/adaptris/interlok/develop/gradle/owasp-exclude.xml" ]
  skipConfigurations = [ "antSql", "spotbugs", "umlDoclet", "offlineJavadocPackages", "javadoc", "jacocoAnt", "jacocoAgent" ]
  formats = [ "HTML", "JUNIT" ]
  junitFailOnCVSS = 7.0
  failBuildOnCVSS = 7.0
  analyzers {
    assemblyEnabled=false
  }
}

check.dependsOn jacocoTestReport
