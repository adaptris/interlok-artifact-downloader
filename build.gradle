plugins {
  id 'java'
  id 'application'
  id 'jacoco'
  id 'eclipse'
  id "org.owasp.dependencycheck" version "7.2.1"
  id 'com.github.spotbugs' version '5.0.12'
  id 'io.spring.dependency-management' version '1.0.14.RELEASE'
  id 'org.springframework.boot' version '2.7.4'
}

ext {
  nexusBaseUrl = project.hasProperty("nexusBaseUrl") ? project.getProperty("nexusBaseUrl") : "https://nexus.adaptris.net/nexus"
  repoUsername = project.hasProperty("repoUsername") ? project.getProperty("repoUsername") : "unknown"
  repoPassword = project.hasProperty("repoPassword") ? project.getProperty("repoPassword") : "unknown"
  componentName = "Interlok Artifact Downloader"
  organizationName = "Adaptris Ltd"
  organizationUrl = "https://www.adaptris.com"
  swaggerVersion = "2.2.3"
  ivyVersion = "2.5.1"
  jacksonBomVersion = "2.13.4.20221013"
}

// Override spring boot dependencies versions
ext["tomcat.version"] = "9.0.63"
ext["jersey.version"] = "2.35"
// ext["slf4j.version"] = "1.7.35"
ext["log4j2.version"] = "2.17.2"
ext["jackson-bom.version"] = jacksonBomVersion
ext["mockito.version"] = "4.5.1"
ext["thymeleaf.version"] = "3.0.15.RELEASE"
ext["snakeyaml.version"] = "1.31"

run.enabled = false
distTar.enabled=false
distZip.enabled=false

sourceCompatibility = JavaVersion.VERSION_1_8
group   = "com.adaptris"
version = "1.0-SNAPSHOT"

repositories {
  mavenCentral()
  maven { url "$nexusBaseUrl/content/groups/public" }
  maven { url "$nexusBaseUrl/content/groups/interlok" }
  maven {
    credentials {
      username repoUsername
      password repoPassword
    }
    url "$nexusBaseUrl/content/groups/private"
  }
}

eclipse {
  classpath {
    downloadJavadoc = true
    downloadSources = true
  }
}

configurations {
  all*.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
  all*.exclude group: "org.springframework.boot", module: "spring-boot-starter-data-jpa"
}

// Use static classpath resources (i.e. in src/main/resources by default) in the live application when running bootRun.
bootRun {
  sourceResources sourceSets.main
}

jar {
  baseName = rootProject.name
  version =  version
  manifest {
    attributes "Built-By": System.getProperty("user.name"),
               "Build-Jdk": System.getProperty("java.version"),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName,
               "Implementation-URL": organizationUrl
  }
}

dependencies {
  implementation "org.springframework.boot:spring-boot-devtools"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-log4j2"
  implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
  implementation "org.springframework.boot:spring-boot-starter-jersey"

  implementation "com.google.guava:guava:31.1-jre"
  implementation (platform("com.fasterxml.jackson:jackson-bom:$jacksonBomVersion"))
  implementation "io.swagger.core.v3:swagger-core:$swaggerVersion"
  implementation "io.swagger.core.v3:swagger-jaxrs2:$swaggerVersion"

  implementation "org.apache.commons:commons-lang3:3.12.0"
  implementation "commons-io:commons-io:2.11.0"

  implementation "org.apache.ivy:ivy:$ivyVersion"

  testImplementation "org.springframework.boot:spring-boot-starter-test"
}

startScripts {
  mainClassName = "com.adaptris.downloader.Application"
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

spotbugsMain {
  effort = "max"
  reportLevel = "high"
  // showProgress = "true"
  // Ignore failures?
  // ignoreFailures = false
  reports {
    xml {
      enabled = false
    }
    html {
      enabled = true
    }
  }
  // includeFilter = new File("$rootDir/gradle/spotbugs-filter.xml")
  // excludeFilter = new File("$rootDir/gradle/spotbugs-exclude.xml")
}

// disable spotbugsTests which checks our test code..
spotbugsTest.enabled = false

dependencyCheck  {
  suppressionFiles= [ "https://raw.githubusercontent.com/adaptris/interlok/develop/gradle/owasp-exclude.xml", "$rootDir/gradle/owasp-exclude.xml" ]
  skipConfigurations = [ "antSql", "spotbugs", "umlDoclet", "offlineJavadocPackages", "javadoc", "jacocoAnt", "jacocoAgent" ]
  formats = [ "HTML", "JUNIT" ]
  junitFailOnCVSS = 7.0
  failBuildOnCVSS = 7.0
  analyzers {
    assemblyEnabled=false
  }
}

check.dependsOn jacocoTestReport
